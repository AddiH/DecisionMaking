model {
  
  # priors
  delta_alpha ~ dnorm(0,.01) # CHECK THIS
  mu_alpha ~ dgamma(0.1,0.1) # CHECK THIS
  
  delta_rho ~ dnorm(0,.01)
  mu_rho ~ dbeta(1,1)T(0.001,0.999)
  
  delta_omega ~ dnorm(0,.01)
  mu_omega ~ dbeta(1,1)T(0.001,0.999)

  # group-level means
  mu_alpha_log_1 <- mu_alpha + (delta_alpha * -0.5)
  mu_rho_probit_1 <- mu_rho + (delta_rho * -0.5) 
  mu_omega_probit_1 <- mu_omega + (delta_omega * -0.5)
  
  # subject-level parameters
  #----------alpha----------
  
  tau_alpha_1 ~ dgamma(0.01,0.01)
  sigma_alpha_1 <- 1/sqrt(tau_alpha_1) 
  
  # reparameterising gamma prior for beliefs about others on first trial 
  log(mu_alpha_1) <- mu_alpha_log_1 # standardisation of initial belief mode - log link
  
  rate_alpha_1 <- ( mu_alpha_1 + sqrt( mu_alpha_1^2 + 4*sigma_alpha_1^2 ) )/
    (2*sigma_alpha_1^2) 
  shape_alpha_1 <- 1 + mu_alpha_1 * rate_alpha_1

  #----------rho----------
  
  sigma_rho_1 ~ dunif(1,100)
  
  probit(mu_rho_1) <- mu_rho_probit_1 # standardisation of rate estimate mean - probit link
  
  shape1_rho_1 <- (mu_rho_1) * sigma_rho_1
  shape2_rho_1 <- (1 - mu_rho_1) * sigma_rho_1
  
  #----------omega----------
  
  sigma_omega_1 ~ dunif(1,100)
  
  probit(mu_omega_1) <- mu_omega_probit_1 # standardisation of rate estimate mean - probit link
  
  shape1_omega_1 <- (mu_omega_1) * sigma_omega_1
  shape2_omega_1 <- (1 - mu_omega_1) * sigma_omega_1
  
  #--------------------group 1 model--------------------
  for (g in 1:ngroups_gr1) {
  
    for (s in 1:groupSize) {
    
      # subject-level parameters
      #----------alpha----------
      alpha_gr1[s,g] ~ dgamma(shape_alpha_1, rate_alpha_1)
      
      #----------rho----------
      rho_gr1[s,g] ~ dbeta(shape1_rho_1, shape2_rho_1)
  
      #----------omega----------
      omega_gr1[s,g] ~ dbeta(shape1_omega_1, shape2_omega_1)

      # Gb for first trial
      Gb_gr1[s,1,g] ~ dpois(alpha_gr1[s,g])
      
      # preference and contribution for first trial
      p_gr1[s,1,g] <- (rho_gr1[s,g]*Gb_gr1[s,1,g])
      c_gr1[s,1,g,1] ~ dpois(p_gr1[s,1,g])
    }
          
    # modelling remaining trials
    for (t in 2:ntrials) {
      
      for (s in 1:groupSize) {
      
        Gb_gr1[s,t,g] <- ((1-omega_gr1[s,g])*(Gb_gr1[s,t-1,g]))+(omega_gr1[s,g]*(Ga_gr1[s,t-1,g,1]))
        p_gr1[s,t,g] <- (rho_gr1[s,g]*Gb_gr1[s,t,g])
        c_gr1[s,t,g,1] ~ dpois(p_gr1[s,t,g])
      }
    }
  }
  #--------------------group 2 model--------------------
  # group-level means
  mu_alpha_log_2 <- mu_alpha + (delta_alpha * 0.5)
  mu_rho_probit_2 <- mu_rho + (delta_rho * 0.5)
  mu_omega_probit_2 <- mu_omega + (delta_omega * 0.5)
  
  # subject-level parameters
  #----------alpha----------
  
  tau_alpha_2 ~ dgamma(0.01,0.01)
  sigma_alpha_2 <- 1/sqrt(tau_alpha_2)
  
  log(mu_alpha_2) <- mu_alpha_log_2 # standardisation of initial belief mode - log link
  
  rate_alpha_2 <- ( mu_alpha_2 + sqrt( mu_alpha_2^2 + 4*sigma_alpha_2^2 ) )/
    (2*sigma_alpha_2^2)
  shape_alpha_2 <- 1 + mu_alpha_2 * rate_alpha_2
  
  #----------rho----------
  
  sigma_rho_2 ~ dunif(1,100)
  
  probit(mu_rho_2) <- mu_rho_probit_2 # standardisation of rate estimate mean - probit link
  
  shape1_rho_2 <- (mu_rho_2) * sigma_rho_2
  shape2_rho_2 <- (1 - mu_rho_2) * sigma_rho_2
  
  #----------omega----------
  
  sigma_omega_2 ~ dunif(1,100)
  
  probit(mu_omega_2) <- mu_omega_probit_2 # standardisation of rate estimate mean - probit link
  
  shape1_omega_2 <- (mu_omega_2) * sigma_omega_2
  shape2_omega_2 <- (1 - mu_omega_2) * sigma_omega_2
  
  for (g in 1:ngroups_gr2) {
  
    for (s in 1:groupSize) {
    
      # subject-level parameters
      #----------alpha----------
      alpha_gr2[s,g] ~ dgamma(shape_alpha_2, rate_alpha_2)
      
      #----------rho----------
      rho_gr2[s,g] ~ dbeta(shape1_rho_2, shape2_rho_2)
  
      #----------omega----------
      omega_gr2[s,g] ~ dbeta(shape1_omega_2, shape2_omega_2)
      
      # Gb for first trial
      Gb_gr2[s,1,g] ~ dpois(alpha_gr2[s,g])
      
      # preference and contribution for first trial
      p_gr2[s,1,g] <- (rho_gr2[s,g]*Gb_gr2[s,1,g])
      c_gr2[s,1,g,1] ~ dpois(p_gr2[s,1,g])
    
    }
    
    # modelling remaining trials
    for (t in 2:ntrials) {
    
      for (s in 1:groupSize) {
      
        Gb_gr2[s,t,g] <- ((1-omega_gr2[s,g])*(Gb_gr2[s,t-1,g]))+(omega_gr2[s,g]*(Ga_gr2[s,t-1,g,1]))
        p_gr2[s,t,g] <- (rho_gr2[s,g]*Gb_gr2[s,t,g])
        c_gr2[s,t,g,1] ~ dpois(p_gr2[s,t,g])
      }
    }
  
  }
}
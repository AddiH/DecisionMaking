model {

  ###### Priors ################
  diff_alpha ~  dnorm(0,1)T(-1,1)
  mu_alpha ~ dnorm(0,1)
  lambda_alpha_gr1 ~ dgamma(.1,.1) 
  lambda_alpha_gr2 ~ dgamma(.1,.1) 
  
  diff_rho ~  dnorm(0,1)T(-1,1)
  mu_rho ~ dnorm(0,1)
  lambda_rho_gr1 ~ dgamma(.1,.1) 
  lambda_rho_gr2 ~ dgamma(.1,.1) 
  
  diff_omega ~  dnorm(0,1)T(-1,1)
  mu_omega ~ dnorm(0,1)
  lambda_omega_gr1 ~ dgamma(.1,.1) 
  lambda_omega_gr2 ~ dgamma(.1,.1) 
  
  #--------------------group 1 model--------------------
  for (g in 1:ngroups_gr1) {
  
    for (s in 1:groupSize) {
    
      # subject-level parameters
      #----------alpha----------
      alpha_gr1[s,g] ~ dnorm((mu_alpha - (diff_alpha/2)),lambda_alpha_gr1)
      
      #----------rho----------
      rho_gr1[s,g] ~ dnorm((mu_rho - (diff_rho/2)),lambda_rho_gr1)T(0.001,)
  
      #----------omega----------
      omega_gr1[s,g]  ~ dnorm((mu_omega - (diff_omega/2)),lambda_omega_gr1)T(0.001,)
      
      # Gb for first trial
      Gb_gr1[s,1,g] ~ dpois(alpha_gr1[s,g])
      
      # preference and contribution for first trial
      p_gr1[s,1,g] <- (rho_gr1[s,g]*Gb_gr1[s,1,g])
      c_gr1[s,1,g,1] ~ dpois(p_gr1[s,1,g])
    }
          
    # modelling remaining trials
    for (t in 2:ntrials) {
      
      for (s in 1:groupSize) {
      
        Gb_gr1[s,t,g] <- ((1-omega_gr1[s,g])*(Gb_gr1[s,t-1,g]))+(omega_gr1[s,g]*(Ga_gr1[s,t-1,g,1]))
        p_gr1[s,t,g] <- (rho_gr1[s,g]*Gb_gr1[s,t,g])
        c_gr1[s,t,g,1] ~ dpois(p_gr1[s,t,g])
      }
    }
  }
  #--------------------group 2 model--------------------
  for (g in 1:ngroups_gr2) {
  
    for (s in 1:groupSize) {
    
      # subject-level parameters
      #----------alpha----------
      alpha_gr2[s,g] ~ dnorm((mu_alpha + (diff_alpha/2)),lambda_alpha_gr2)
      
      #----------rho----------
      rho_gr2[s,g] ~ dnorm((mu_rho + (diff_rho/2)),lambda_rho_gr2)T(0.001,)
  
      #----------omega----------
      omega_gr2[s,g]  ~ dnorm((mu_omega + (diff_omega/2)),lambda_omega_gr2)T(0.001,)
      
      # Gb for first trial
      Gb_gr2[s,1,g] ~ dpois(alpha_gr2[s,g])
      
      # preference and contribution for first trial
      p_gr2[s,1,g] <- (rho_gr2[s,g]*Gb_gr2[s,1,g])
      c_gr2[s,1,g,1] ~ dpois(p_gr2[s,1,g])
    
    }
    
    # modelling remaining trials
    for (t in 2:ntrials) {
    
      for (s in 1:groupSize) {
      
        Gb_gr2[s,t,g] <- ((1-omega_gr2[s,g])*(Gb_gr2[s,t-1,g]))+(omega_gr2[s,g]*(Ga_gr2[s,t-1,g,1]))
        p_gr2[s,t,g] <- (rho_gr2[s,g]*Gb_gr2[s,t,g])
        c_gr2[s,t,g,1] ~ dpois(p_gr2[s,t,g])
      }
    }
  
  }
}